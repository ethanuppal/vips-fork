module Arith #(
    param Width: u32 = 4,
) (
    a  : input  logic<Width>,
    b  : input  logic<Width>,
    sub: input  logic       ,
    r  : output logic<Width>,
    v  : output logic       ,
    c  : output logic       ,
) {
    /* verilator lint_off UNOPTFLAT */
    var ca: logic<Width + 1>;

    assign ca[0] = sub;
    assign v     = ca[msb] ^ ca[msb - 1];
    assign c     = ca[msb];

    for i in 0..Width :label {
        var cl: logic;
        assign cl = ca[i];
        inst full_add: FullAdder (
            a    : a[i]      ,
            b    : b[i] ^ sub,
            c    : cl        ,
            sum  : r[i]      ,
            carry: ca[i + 1] ,
        );
    }
    /* verilator lint_on UNOPTFLAT */
}

#[test(arith)]
embed (inline) sv{{{
    module test;
        logic [3:0] a;
        logic [3:0] b;
        logic sub;
        logic [3:0] r;
        logic v;
        logic c;

        vips_Arith arith (a, b, sub, r, v, c);

        initial begin
            a = 0; b = 0; sub = 0;
            #10;
            assert (r == 0 & v == 0 & c == 0) else $error("0+0");

            a = 5; b = 2; sub = 0;
            #10;
            assert (r == 7 & v == 0 & c == 0) else $error("5+2");

            a = 5; b = 3; sub = 0;
            #10;
            // 0111
            //  0101
            //  0011
            // 01000
            assert (r == 8 & v == 1 & c == 0) else $error("5+3, V ", r, " ", v, " ", c);

            a = 5; b = 10; sub = 0;
            #10;
            //  0
            //  0101 5
            //  1010 -6
            // 01111
            assert (r == 15 & v == 0 & c == 0) else $error("5+10 ", r, " ", v, " ", c);

            a = 5; b = 11; sub = 0;
            #10;
            //  111
            //  0101 5
            //  1011 -5
            // 10000
            assert (r == 0 & v == 0 & c == 1) else $error("5+11 ", r, " ", v, " ", c);

            a = 5; b = 3; sub = 1;
            #10;
            assert (r == 2 & v == 0 & c == 1) else $error("5-3 C", r, " ", v, " ", c);

            a = 5; b = 6; sub = 1;
            #10;
            assert (r == 15 & v == 0 & c == 0) else $error("5-6 C", r, " ", v, " ", c);

            a = 5; b = -3; sub = 1;
            #10;

            // 0111
            //  0101 5
            //  0011 (--3)
            //  ----
            // 01000
            assert (r == 8 & v == 1 & c == 0) else $error("5-(-3) C", r, " ", v, " ", c);

            $finish;
        end
    endmodule
}}}
